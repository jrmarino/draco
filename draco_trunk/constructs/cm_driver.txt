##
##  DRACO ADA COMPILER
##  CMAKE BUILD SCRIPT - DRACO DRIVER
##
##
##  Copyright (c) 2010, John Marino (www.auroraux.org)
##  All rights reserved.
##
##  Permission to use, copy, modify, and/or distribute this software for any
##  purpose with or without fee is hereby granted, provided that the above
##  copyright notice and this permission notice appear in all copies.
##
##  THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
##  WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
##  MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
##  ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
##  WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
##  ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
##  OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
##



############## six_digit_os_version ##############
# This function look for numbers at the end of the target triplet and
# creates a zero padded version from that.  If no version is found
# 000000 is passed back

function (six_digit_os_version)

   set (major "0")
   set (minor "00")
   set (third "000")
   if (${TARGET_TRIPLET} MATCHES "^.*-.*-[^.]*[0-9]+([.][0-9]+)*$")

      # try third first
      if (${TARGET_TRIPLET} MATCHES "^.*-.*-[^.]*[0-9]+[.][0-9]+[.][0-9]+$")
         string (REGEX      REPLACE "^.*-.*-[^.]*[0-9]+[.][0-9]+[.]([0-9]+)$" "\\1" teststr ${TARGET_TRIPLET})
         string(LENGTH "${teststr}" strlen)
         if (strlen LESS 2)
            set (third "00${teststr}")
         elseif (strlen LESS 3)
            set (third "0${teststr}")
         else ()
            set (third ${teststr} )
         endif ()
      endif ()
      # try minor now
      if (${TARGET_TRIPLET} MATCHES "^.*-.*-[^.]*[0-9]+[.][0-9]+([.][0-9]+)?$")
         string (REGEX      REPLACE "^.*-.*-[^.]*[0-9]+[.]([0-9]+)([.][0-9]+)?$" "\\1" teststr ${TARGET_TRIPLET})
         string(LENGTH "${teststr}" strlen)
         if (strlen LESS 2)
            set (minor "0${teststr}")
         else ()
            set (minor ${teststr})
         endif ()
      endif ()
      # try major now
      if (${TARGET_TRIPLET} MATCHES "^.*-.*-[^.]*[0-9]+([.][0-9]+)*$")
         string (REGEX      REPLACE "^.*-.*-[^.]*([0-9]+)([.][0-9]+)*$" "\\1" major ${TARGET_TRIPLET})
      endif ()
   endif ()

   set (result_six_digits "${major}${minor}${third}" PARENT_SCOPE)

endfunction (six_digit_os_version)





############## construct_configuration ##############
# This output of this function is to display which configuration options were requested
# at configuration time.  These are the -D macro values passed to "cmake".

function (construct_configuration)

   set (list_of_options
            profile
            prefix
            host
            target
            ada_boot
            toolpath
            with_clang
            c_cross
            with_as
            with_ld
            non_gnu_as
            non_gnu_ld
            enable_nls
            with_gmp
            with_mpc
            with_mpfr
            with_iconv
            with_sysroot
            driver_suffix
   )
   set (tmp_result "")
   foreach (element ${list_of_options})

      if (DEFINED ${element})
            list (APPEND tmp_result "${element}=${${element}}")
      endif ()

   endforeach ()

   if ("${tmp_result}" STREQUAL "")
      set (result_config "No options were configured away from their defaults." PARENT_SCOPE)
   else ()
      string (REPLACE ";" " " clean_result "${tmp_result}")
      set (result_config "${clean_result}" PARENT_SCOPE)
   endif ()

endfunction (construct_configuration)




############## assembler_path ##############
# If we are at this point, which is past the header generation, we know that our
# with_as (if present) is a good value.  We can derive the full path.

function (assembler_path)

   if (DEFINED with_as)
      set (draco_assembler "${with_as}" PARENT_SCOPE)
   else ()
      if ("${gas}" STREQUAL "yes")
         set (stdname gas)
      else ()
         set (stdname as)
      endif ()
      find_path (as_path ${stdname})
      if ("${as_path}" STREQUAL "as_path-NOTFOUND")
         message (FATAL_ERROR "There is no assembler named `${stdname}` in the search path.")
      else ()
         set (draco_assembler "${as_path}/${stdname}" PARENT_SCOPE)
      endif ()
   endif ()

endfunction (assembler_path)


############## create_driver_specification ##############
# This function creates a configuration file for building the DRACO driver
# While the target information doesn't change, each stage needs a copy.
# We could just copy from stage on on later stages, but if we implement a
# "lean" build, it might not be available.  It's a cheap text file creation
# operation, so for robustness, just build a new version each time.

function (create_driver_specification)

   string (STRIP "${DATESTAMP}" S_DATESTAMP)
   string (STRIP "${BASEVER}"   S_BASEVER)
   string (REGEX REPLACE "^([0-9]+[.][0-9]+)([.][0-9]+)?$" "\\1" SHORTVER ${S_BASEVER})
   if (${gas} STREQUAL "yes")
      set (GAS_OPTION "True")
   else ()
      set (GAS_OPTION "False")
   endif ()
   set (commpath "libexec/draco-${DRACO_VERSION}")
   set (execpath "../${commpath}:${draco_prefix}/${commpath}")
   six_digit_os_version ()
   construct_configuration ()
   assembler_path ()

   file (WRITE ${outfile} "
with DracoSystem;
package DracoSystemSpecs is

   Native_System : constant DracoSystem.RecSystem := (\n")

   file (APPEND ${outfile} "      Null_File_Type => DracoSystem.${draco_bitbucket},\n")
   file (APPEND ${outfile} "      Backend        => DracoSystem.${draco_backend},\n")
   file (APPEND ${outfile} "      Draco_Version  => DracoSystem.Set_Version (\"${DRACO_VERSION}\"),\n")
   file (APPEND ${outfile} "      Gnat_Version   => DracoSystem.Set_Version (\"${SHORTVER}.${S_DATESTAMP}\"),\n")
   file (APPEND ${outfile} "      MachineTarget  => DracoSystem.Set_Target  (\"${TARGET_TRIPLET}\"),\n")
   file (APPEND ${outfile} "      Architecture   => DracoSystem.Set_Arch    (\"${cpu_type}\"),\n")
   file (APPEND ${outfile} "      OS_Name        => DracoSystem.Set_OSName  (\"${draco_osname}\"),\n")
   file (APPEND ${outfile} "      OS_Version     => ${result_six_digits},\n")
   file (APPEND ${outfile} "      Have_GNU_AS    => ${GAS_OPTION},\n")
   file (APPEND ${outfile} "      Dash_For_Pipe  => ${draco_dash_pipe},\n")
   file (APPEND ${outfile} "      CPU_AutoDetect => False,\n")
   file (APPEND ${outfile} "      CC_Flags       => DracoSystem.${draco_cc1_spec},\n")
   file (APPEND ${outfile} "      Path_assembler => DracoSystem.Set_DefPath (\"${draco_assembler}\"),\n")
   file (APPEND ${outfile} "      Path_libexec   => DracoSystem.Set_DefPath (\"${execpath}\"),\n")
   file (APPEND ${outfile} "      Config_options => DracoSystem.Set_Config  (\"${result_config}\")\n")

   file (APPEND ${outfile} "   );\n\nend DracoSystemSpecs;\n\n")

endfunction (create_driver_specification)




############## define_gnat_tool_paths ##############
# This function will check to see if the toolpaths option was issued and if so
# it will check its validity.  Otherwise it will search for both gnatlink and
# gnatbind in the default search path.  If both are not found, a fatal error
# will be issued.  The paths will be stored in the cache.

function (define_gnat_tool_paths)

   if ("${toolpath}" STREQUAL "")
      find_program (TOOL_BIND gnatbind)
      find_program (TOOL_LINK gnatlink)
   else ()
      find_program (TOOL_BIND gnatbind PATHS "${toolpath}" NO_DEFAULT_PATH)
      find_program (TOOL_LINK gnatlink PATHS "${toolpath}" NO_DEFAULT_PATH)
   endif ()

   if ("${TOOL_BIND}" STREQUAL "TOOL_BIND-NOTFOUND")
      if (DEFINED toolpath)
         message (FATAL_ERROR "The `gnatbind` program was not found in the `${toolpath}` directory.")
      else ()
         message (FATAL_ERROR "The `gnatbind` program was not found in the standard search path.")
      endif ()
   endif ()
   if ("${TOOL_LINK}" STREQUAL "TOOL_LINK-NOTFOUND")
      if (DEFINED toolpath)
         message (FATAL_ERROR "The `gnatlink` program was not found in the `${toolpath}` directory.")
      else ()
         message (FATAL_ERROR "The `gnatlink` program was not found in the standard search path.")
      endif ()
   endif ()

endfunction (define_gnat_tool_paths)




############## define_external_ada_compiler ##############
# This function will look for an Ada compiler named draco or gnatgcc.
# If the parameter "ada_boot" is provided, then it will look in that exact
# path and store it in the cache if successful.  If not, fatal error is thrown.

function (define_external_ada_compiler)

   if ("${ada_boot}" STREQUAL "")
      find_program (EXTERNAL_ADA_COMPILER gnatgcc draco)
      if ("${EXTERNAL_ADA_COMPILER}" STREQUAL "EXTERNAL_ADA_COMPILER-NOTFOUND")
         message (FATAL_ERROR "The `draco` or `gnatgcc` compilers could not be found in the standard search path.")
      endif ()
   else ()
      if (EXISTS "${ada_boot}")
         set (EXTERNAL_ADA_COMPILER "${ada_boot}" CACHE FILEPATH "External Ada compiler provided via command-line" FORCE)
      else ()
         message (FATAL_ERROR "There was no file located at `${ada_boot}` (Ada compiler or otherwise).")
      endif ()
   endif ()

endfunction (define_external_ada_compiler)




############## compile_and_mark_objects ##############
# This function is the major one.  It adds the object file dependencies to
# the driver_stage_x target, and it compiles each file to an object file.
# The bind and linking has to come at build time. (?)

function (compile_and_mark_objects X ADA_COMPILER)

   set (ali_files "")

   foreach (encoded_name ${ordered_files})

      string (REPLACE ":" ";" test ${encoded_name})

      list (GET test 0 location)
      list (GET test 1 extension)
      list (GET test 2 basename)
      list (APPEND ali_files ${basename}.ali)

      if ("${location}" STREQUAL "R")
         set (srcfile ${DIR_DRIVER}/${basename}.${extension})
         set (incflag "-I${CMAKE_CURRENT_BINARY_DIR}")
      else ()
         set (srcfile ${basename}.${extension})
         set (incflag "-I-;-I${DIR_DRIVER}")
      endif ()

      add_custom_command (
         OUTPUT ${basename}.o ${basename}.ali
         COMMAND ${ADA_COMPILER} ${incflag} -c ${srcfile}
         DEPENDS ${srcfile}
      )

   endforeach()

   add_custom_command (
      OUTPUT b~draco.ads b~draco.adb
      COMMAND ${TOOL_BIND} -x draco.ali
      DEPENDS ${ali_files}
   )

   add_custom_command (
      OUTPUT draco
      COMMAND ${TOOL_LINK} draco.ali -o ${execloc}
      DEPENDS b~draco.ads b~draco.adb
   )

endfunction (compile_and_mark_objects)






# set location for draco system specification
set (outfile "dracosystemspecs.ads")


# these are the base file names for the .adb files that make up
# the driver.  All have specification files except the
# main "draco.adb" file and dracosystemspecs.ads has no body.


set (ordered_files
      R:adb:dracosystem
      L:ads:dracosystemspecs
      R:adb:pathinfo
      R:adb:pipemechanism
      R:adb:switchmap
      R:adb:driverswitch
      R:adb:commands
      R:adb:draco
)


create_driver_specification ()
define_gnat_tool_paths ()
define_external_ada_compiler ()


if (${CURRENT_STAGE} EQUAL 1)

   set (pass_compiler ${EXTERNAL_ADA_COMPILER})
   set (execloc       ${DIR_STAGE_1}/sysroot/bin/draco)
   add_custom_target (driver_stage1 DEPENDS ${outfile} draco)
   add_dependencies  (draco_stage1 driver_stage1)

elseif (${CURRENT_STAGE} EQUAL 2)

   set (pass_compiler ${DIR_STAGE_1}/sysroot/bin/draco)
   set (execloc       ${DIR_STAGE_2}/sysroot/bin/draco)
   add_custom_target (driver_stage2 DEPENDS ${outfile})
   add_dependencies  (draco_stage2 driver_stage2)

else (${CURRENT_STAGE} EQUAL 3)

   set (pass_compiler ${DIR_STAGE_2}/sysroot/bin/draco)
   set (execloc       ${DIR_STAGE_3}/sysroot/bin/draco)
   add_custom_target (driver_stage3 DEPENDS ${outfile})
   add_dependencies  (draco_stage3 driver_stage3)

endif ()


compile_and_mark_objects ("${CURRENT_STAGE}" "${pass_compiler}")



